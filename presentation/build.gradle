plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
    id 'org.flywaydb.flyway' version '9.5.1'
    id "io.sentry.jvm.gradle" version "5.12.2"
}

group = 'com.github.veloproject.socialmediaservices'
version = '0.0.1-SNAPSHOT'

ext {
    grpcVersion = '1.65.0'
    protobufVersion = '3.25.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

springBoot {
    mainClass = 'com.github.veloproject.socialmediaservices.presentation.PresentationApplication'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'

    implementation 'net.devh:grpc-server-spring-boot-starter:3.0.0.RELEASE'
    implementation 'net.devh:grpc-client-spring-boot-starter:3.0.0.RELEASE'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    implementation 'com.google.cloud:google-cloud-storage:2.56.0'
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE"
    implementation 'javax.annotation:javax.annotation-api:1.3.2' // For @Generated annotation used by protoc

    implementation platform("io.sentry:sentry-bom:8.27.1")
    implementation "io.sentry:sentry-opentelemetry-agent"

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    runtimeOnly 'org.postgresql:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation project(":application")
    implementation project(":domain")
    implementation project(":infrastructure")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java',
                    'build/generated/source/proto/main/grpc'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

sentry {
    org = "velo-93"
    projectName = "velo-social-media-service"
}

flyway {
    url = 'jdbc:postgresql://db:5433/social_media_services'
    user = 'postgres'
    password = 'root'
    locations = ['filesystem:presentation/src/main/resources/db/migration']
    table = 'social_media_services_flyway_schema_history'
}

tasks.named("generateSentryBundleIdJava").configure {
    dependsOn("generateProto")
}

tasks.named("sentryCollectSourcesJava").configure {
    dependsOn("generateProto")
}
